plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'gg.jte.gradle' version '3.1.15'
}

group = 'ru.mentee.power'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.12.0'
    assertjVersion = '3.26.3'
    lombokVersion = '1.18.38'
    mockitoVersion = '5.21.0'
    servletVersion = '6.1.0'
    tomcatVersion = '11.0.1'
    jteVersion = '3.1.15'
}


dependencies {
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    implementation "jakarta.servlet:jakarta.servlet-api:${servletVersion}"
    implementation "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    implementation "gg.jte:jte:${jteVersion}"
    implementation "gg.jte:jte-runtime:${jteVersion}"
}

jte {
    sourceDirectory = file("src/main/jte").toPath()
    contentType = "Html"
    binaryStaticContent = false
}

application {
    mainClass = 'ru.mentee.power.crm.Main'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file('config/checkstyle/checkstyle.xml')

}

tasks.named('checkstyleMain') {
    ignoreFailures = true
}

tasks.named('checkstyleTest') {
    ignoreFailures = true
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'ru/mentee/power/crm/web/HelloCrmServer*',
                    'ru/mentee/power/crm/Main*',
                    'ru/mentee/power/crm/model/Lead*',
                    'ru/mentee/power/crm/model/LeadStatus*',
                    'ru/mentee/power/crm/domain/Repository*'
            ])
        }))
    }
}


jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            excludes = [
                    'ru.mentee.power.crm.web.HelloCrmServer*',
                    'ru.mentee.power.crm.Main*',
                    'ru.mentee.power.crm.model.Lead*',
                    'ru.mentee.power.crm.model.LeadStatus*',
                    'ru/mentee/power/crm/domain/Repository*'
            ]

            limit {
                minimum = 0.7
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}

tasks.withType(Checkstyle) {
    enabled = false
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
