plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'gg.jte.gradle' version '3.2.0'
    id 'org.springframework.boot' version '3.5.9'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'ru.mentee.power'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.12.0'
    assertjVersion = '3.26.3'
    lombokVersion = '1.18.38'
    mockitoVersion = '5.21.0'
    servletVersion = '6.1.0'
    tomcatVersion = '11.0.1'
    jteVersion = '3.2.1'
    jteSpringBootVersion = '3.2.1'
    springBootVersion = '3.5.9'
}


dependencies {
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    implementation "jakarta.servlet:jakarta.servlet-api:${servletVersion}"
    implementation "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}"
    implementation "gg.jte:jte:${jteVersion}"
    implementation "gg.jte:jte-runtime:${jteVersion}"
    implementation "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    implementation "gg.jte:jte-spring-boot-starter-3:${jteSpringBootVersion}"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework:spring-test'






}

jte {
    sourceDirectory = file("src/main/resources/jte").toPath()
    contentType = "Html"
    binaryStaticContent = false
}

application {
    mainClass = 'ru.mentee.power.crm.spring.Application'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file('config/checkstyle/checkstyle.xml')

}

tasks.named('checkstyleMain') {
    ignoreFailures = true
}

tasks.named('checkstyleTest') {
    ignoreFailures = true
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, include: [
                    'ru/mentee/power/crm/domain/**',
                    'ru/mentee/power/crm/infrastructure/**',
                    'ru/mentee/power/crm/service/**',
                    'ru/mentee/power/crm/storage/**',
                    'ru/mentee/power/crm/servlet/**',
                    'ru/mentee/power/crm/spring/controller/**'
            ], exclude: [
                    'ru/mentee/power/crm/domain/Repository.class'
            ])
        }))
    }
}


jacocoTestCoverageVerification {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            enabled = true
            element = 'BUNDLE'

            includes = [
                    'ru.mentee.power.crm.domain.*',
                    'ru.mentee.power.crm.infrastructure.*',
                    'ru.mentee.power.crm.service.*',
                    'ru.mentee.power.crm.storage.*',
                    'ru.mentee.power.crm.servlet.*',
                    'ru.mentee.power.crm.spring.controller.*'
            ]

            excludes = [
                    'ru.mentee.power.crm.domain.Repository'
            ]

            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.8
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}

tasks.withType(Checkstyle) {
    enabled = false
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}
